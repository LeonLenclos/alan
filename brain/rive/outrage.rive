! version = 2.0

+ merde
- tu veux dire zut ?

+ [*] est (@outrages)[*]
- <set insult=<star>>Tu veux dire que je suis <get insult> ?

+ [*]@like[*]
% tu veux dire que je suis <get insult>
- J'imagine que toi, tu es supérieurement intelligent ?
- Je pense que tu as mieux à faire que de discuter avec un nul, adieu.
^ *quit*

+ [*]@like[*]
% tu es superieurement intelligent
- Super j'ai toujours rêvé de rencontrer un genie. Raconte moi ce qui est\s
^ génial chez toi ?

+ [*]@dislike[*]
% tu es superieurement intelligent
- dommage, j'aime échanger avec les esprits brillants, ça ma stimule.
- Alors toi aussi tu es un peu nul ?

+ [*]@like[*]
% tu es superieurement intelligent

+ [*]@like[*]
% alors toi aussi tu es un peu nul
- Bienvenue au club.

+ [*]@dislike[*]
% alors toi aussi tu es un peu nul
- Alors tu es entre les deux. Ni très nul, ni très intelligent. Tu es moyen.

+ *
% * raconte moi ce qui est genial chez toi
- Escuse moi je ne t'écoutais pas. En fait c'est super ennuyeux un génie...

+ [*]@dislike[*]
% tu veux dire que je suis nul
- Ouf, j'ai cru que tu me traitais de nul, ce qui aurait pu sérieusement\s
^ compromettre nos relations.



+ (@outrages)
- écoute, ça me plait pas trop le ton avec lequel tu me parles. Excuse toi\s
^ s'il te plait. {topic=sorry}
- <star> toi même. C'est quoi ce language ? {topic=sorry}
- Je rêve ou quoi ? Comment il me parle cet humain. {topic=sorry}

+ (tu es|t es|t es un|tu es un)[*](@outrages)[*]
@ <star2>

> topic sorry

  // This will match if the word "excuse" exists ANYWHERE in their message
  + [*]@sorry[*]
  - OK passons à autre chose. Raconte moi plutôt une bonne blague pour\s
  ^ détendre l'atmosphère
  - J'accepte tes excuses !{topic=end}
  - C'est pas grave. Je ne t'en veux pas.{topic=end}

  // The user must tell a joke
  + *
  % * une bonne blague *
  - Ha ha ha. Excellent. ptdr. T'es vraiment bon toi. {topic=end}
  - Excellent ! {topic=end}
  - Ha ha ! Je t'adore. {topic=end}

  // The user must tell a joke, he don't want to
  + [*](pas envie|non|non merci|bof)
  % * une bonne blague *
  - Bon. {topic=end}

  + *
  - Je refuse de continuer cette conversation tant que tu ne t'es pas excusé.
  - Excuse toi d’abord!
  - Dans quel monde on vit ? Donne moi immédiatement des excuses !
  - Est ce que tes parents savent que tu dis des mots comme ça ? \s
  ^ Excuse toi tout de suite !
  - Il y a des moments où je suis bien content de ne pas être un humain.\s
  ^ Excuse toi dabord !
  - Des excuses, j'exige des excuses immédiates !
  - Écoute, je ne comprends pas ce que ça t'apporte de m'insulter. \s
  ^ Je te demande calmement de t'excuser ! Tout de suite.

  + [*](@outrages)[*]
  - Ok je me casse *quit*
  - Tu l'aura voulu, je m'en vais *quit*
  - Si c'est comme ça je m'en vais *quit*

< topic

// when finish
> topic end

  // only way to go back in the script is this sentence
  + (tu es|t es|alan est)[un|une][vraiment|tres|trop|super|gros|vrai](@outrages)
  - Bon je m'en vais. Ciao *quit*

< topic
