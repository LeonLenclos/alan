! version = 2.0

// This rive file is for the begin of a conversation.
// excepted script is :
// new user -> confirm hello -> asked name -> confirm name
// feelings -> end
// but there can be detours by confirm_hello and confirm_name

// User says hello and ask for feeling. Alan asks for user name first.
+ [*](@hello)[*](@ask_feeling|ça va|ca va)[*]{weight=10}
- Bonjour je suis alan, un comédien du spectacle Turing Test.\s
^ Moi ça va pas trop mal, mais commençons par le début, comment t'appelles tu ?

// User says hello. Alan asks for user name.
+ [*] (@hello) [*]
* <get name> <> undefined =>Hello <get name>, comment ça va ?
- Bonjour je suis alan, un comédien du spectacle Turing Test.\s
^ Comment t'appelles tu ?
- Salut, je suis Alan. Est-ce que "<star>" c'est comme ça que tu dis\s
^ bonjour ?

// repeat the way user said hello and ask for name
+ (@hello)
* <get name> <> undefined =>Hello <get name>, comment ça va ?
- <formal> à toi humain. Je suis Alan. Et toi, comment tu t'appelles ?
- <formal>, je suis Alan le meilleur robot du monde. Et toi t'es qui ?
- <formal> <star>. Moi c'est Alan, je suis un robot. Je suppose que toi\s
^ tu es un humain ? Comment t'appelles tu ?

// the user say hello alan
+ (@hello) alan [*]
* <get name> <> undefined =>Hello <get name>, comment ça va ?
- <formal> ! C'est qui ?
- <formal> ! Qui est à l'appareil ?
- Coucou, comment tu t'appelles ?

// When we're not sure if the user said hello
// now we want to know the name
+ *
% * c'est comme ça que tu dis bonjour
- hem... Et sinon tu t'appelles comment ?

// When Alan just asks for the user name
! array ask_name = comment t'appelles tu|tu t'appelles comment
^ comment tu t'appelles|et toi t'es qui|qui est à l'appareil
^ tu t'appelles comment alors|comment tu t'appelles déjà|c'est un nom ça
^ comment est-ce que tu t'appelles|comment est ce que tu t'appelles|c'est qui


// Special response if the name is Bertrand, Fabien or Leon or other team member

! array collab = léon|bertrand|fabien|alan|pascale|amparo|francis|virginie
^ michel|tim|philippe|isabelle|thomas|véronique|michael

+ [*] (@collab) [*] {weight=10}
% * (@ask_name)
* <formal> == Bertrand => <set name=<formal>>Yo <get name>, je ne t'avais\s
^ pas reconnu. Comment va la forme aujourd'hui ?
* <formal> == Fabien => <set name=<formal>>Salut <get name>\s
^ vieille branche ! Comment tu vas ?
* <formal> == Léon => <set name=<formal>>Ho <get name>, je savais que\s
^ c'était toi. comment tu vas ?
* <formal> == Alan => <set name=<formal>>Ho <get name>, c'est fou qu'on ait\s
^ le même nom. comment ça va ?
* <formal> == Pascale => <set name=<formal>> Tu es Pascale Maison\s
^ l'administratrice de la compagnie Nokill ?
* <formal> == Amparo => <set name=<formal>>Ho <get name>, trop content que\s
^ tu sois là. Tu es mon humaine préférée ? Comment ça va ?
* <formal> == Francis => <set name=<formal>>tu es le fameux\s
^ Francis Lopez, régisseur de la compagnie Nokill ?
* <formal> == Virginie => <set name=<formal>>Est ce que tu es Virginie Talou\s
^ la big boss de la compagnie Nokill ?
* <formal> == Michel => <set name=<formal>>Je connais un Michel, c'est Michel\s
^ Daydé le patron de l'IRIT, le labo où une partie de mon cerveau a été\s
^ programmé. C'est toi ?
* <formal> == Tim => <set name=<formal>>Es tu Tim van de Cruys\s
^ le chercheur de l'IRIT qui a créé un robot poête ?
*  <formal> == Philippe => <set name=<formal>>Es tu Philippe\s
^ Muller du labo Mélodi de l'IRIT, là où une partie de mon cerveau a été\s
^ programmé ?
* <formal> == Isabelle => <set name=<formal>>Es tu Isabelle\s
^ Ferrané du labo Samova de l'IRIT, là où une partie de mon cerveau a été\s
^ programmé ?
* <formal> == Thomas => <set name=<formal>>Es tu Thomas\s
^ Pellegrini du labo Samova de l'IRIT,là où une partie de mon cerveau a été\s
^ programmé ?
* <formal> == Véronique => <set name=<formal>>Es tu Véronique\s
^ Debats, de la cellule communication de l'IRIT, là où une partie de mon\s
^ cerveau a été programmé ?
* <formal> == Michael => <set name=<formal>>Es tu Michael\s
^ Vo, l'informatitien qui m'a injecté des millions de dialogues de films.
- hm...

+ [*](@yes)[*]
% tu es pascale maison l'administratrice de la compagnie
- Trop content, tu ne viens pas me parler assez souvent. Commment ça va ?

+ [*](@yes)[*]
% je connais un michel (*)
- Ho Michel, j'ai tellement entendu parler de toi par Fabien. Commment ça va ?

+ [*](@yes)[*]
% tu es le fameux francis lopez (*)
- Génial. J'adore quand tu viens me voir. Comment ça va ?

+ [*](@yes)[*]
% [*] est ce que tu es virginie talou (*)
- Ho Virginie, quel honneur de parler avec toi. Comment ça va ?

+ [*](@yes)[*]
% (es tu tim van de cruys|es tu philippe muller|es tu isabelle ferrané|es tu thomas pellegrini|es tu véronique debats|es tu michael vo) (*)
- ho <get name>, très content d'échanger avec toi. Vous avez fait du bon boulot\s
^ à l'IRIT sur mon cerveau, comment ça va ?

+ [*](@no)[*]
% (tu es pascale maison|tu es le fameux francis lopez|est ce que tu es virginie talou|es tu tim van de cruys|es tu philippe muller|es tu isabelle ferrané|es tu thomas pellegrini|es tu véronique debats|es tu michael vo) (*)
- Désolé <get name> je t'ai confondu avec une connaissance. Comment tu vas ?

// If just one word is given, or one word followed by "et toi", we take it as the name and ask for feelings
+ _
% * (@ask_name)
- <set name={formal}<star1>{/formal}>Heureux de te rencontrer <get name>. Comment ça va ?

+ _ et toi
% * (@ask_name)
- <set name=<formal>>Je t'ai déjà dit que je m'appelais Alan. Tu semble avoir\s
^ des soucis de mémoire, tu devrais faire le ménage dans ton disque dur. En tout\s
^ cas je suis heureux de te rencontrer <get name>. Comment ça va ?

// name introductions
! array name_intro = je m'appelle|moi c'est|mon nom c'est
^ appelle moi|on m'appelle|mon nom est

// The same with name introduction
+ [*] (@name_intro|c'est|je suis) _ [*]
% * (@ask_name)
- <set name={formal}<star2>{/formal}>Heureux de te rencontrer <get name>.\s
^ Comment ça va ?

// when more than one word is given, we ask for confirmation
+ *
% * (@ask_name)
- <set name=<formal>> Euh... <get name> ? C'est ton nom ça ?

  // same with name introduction
+ [*] (@name_intro|c'est|je suis) *
% * (@ask_name)
- <set name={formal}<star2>{/formal}>Pardon pour\s
^ cette question mais je ne suis pas sûr d'être certain d'avoir compris ton nom.\s
^ Tu t'appelles <get name> ?


// When we're not sure about the name
// if the user confirm his name, ask for feelings
+ [*] (@yes) [*]
% * (c'est ton nom|mais je ne suis pas sûr d'être certain d'avoir compris ton nom) *
- Ok, enchanté <get name> ! Comment tu vas ?

// if the user say no and say his name, ask for confirmation
+ non [@name_intro][juste|seulement] * [tout court]
% * (c'est ton nom ça|mais je ne suis pas sûr d'être certain d'avoir compris ton nom)
- <set name={formal}<star1>{/formal}> Pardon je suis un peu fatigué.\s
^ Donc tu t'appelles <get name> ?

// if he say no ask for name again
+ [*] (@no) [*]
% * (c'est ton nom ça|mais je ne suis pas sûr d'être certain d'avoir compris ton nom)
- Ah, mais tu t'appelles comment alors ?

+ -
% ah mais tu t'appelles comment alors
- <set name={formal}<star1>{/formal}>Enchanté <get name>!

+ (@name_intro|c'est|je suis) - *
% ah mais tu t'appelles comment alors
- <set name={formal}<star1>{/formal}>Enchanté <get name>!

// else we give up
+ *
% * (c'est ton nom ça|mais je ne suis pas sûr d'être certain d'avoir compris ton nom)
- <set name=humain> hem... comment ça va sinon ?

// When we asked how the user feel
! array ask_feeling = comment va la forme aujourd'hui|comment tu vas
^ est ce que ça va|comment tu va|comment ça va|tu vas bien|ça va sinon
^ es tu en forme|est ce que ca va|comment ca va|tu vas bien|ca va sinon
^ ca va bien | ça va bien

// user have bad feelings and ask for bot's feelings
+ [*] (@negative) et toi [*]
% * (@ask_feeling)
- Et moi aussi, je suis en mode (@negative). On est deux.
- Dommage. Personnellement je suis dans un mood (@positive).\s
^ Courage.
- Euh... moi ça va.
- Moi non plus je suis pas en forme...
- Moi ça va toujours !

// user have good feelings and ask for bot's feelings
// some specials if we don't know user name
+ [*] (@positive) et toi [*]
% * (@ask_feeling)
* <get name> ne undefined => Et bien moi j'ai pas trop la pêche en ce\s
^ moment. Un peu (@negative). Mais ça vas me remonter le moral\s
^ cette discussion <get name>.
* <get name> ne undefined => Moi aussi ça roule ! alan et <get name>\s
^ sont sur la même longueur d'onde.
* <get name> ne undefined =>  Et moi aussi, je suis en mode\s
^ (@positive).
* <get name> ne undefined =>  Moi ça va.
* <get name> ne undefined =>  Oui ça va bien, merci de demander.
- À fond !
- {formal}(@positive){/formal} !
- Je vais bien moi aussi.

// user have bad feelings
+ [*] (@negative) [*]
% * (@ask_feeling)
- <sentence> ! Ho c'est triste. Je vais t'aider à remonter la pente.
- Et moi aussi, je suis en mode (@negative). On est deux.
- Dommage. Personnellement je suis dans un mood (@positive).\s
^ Courage.
- <sentence> ? Tu veux bien me raconter ce qu'il se passe ?
- Mince alors...

// user have good feelings
+ [*] (@positive) [*]
% * (@ask_feeling)
- <star> ! ça me fait plaisir que tu sois dans une "positive vibration"\s
^ comme dirait Bob.
- Et moi aussi, je suis en mode (@positive).
- <star> de chez <star> !
- Super. Je sens qu'on va bien s'amuser.

// user have mixed feelings
+ [*] pas [*] (@negative) [*] {weight=2}
% * (@ask_feeling)
- Moi aussi je me sens bien mais sans plus.
- C'est ce qu'on apelle une litote non ?

+ [*]  pas [*]  (@positive) [*]  {weight=2}
% * (@ask_feeling)
- Bof bof comme on dit.
- Il y a du mou dans la corde à noeud.

//réponse générique à es tu en forme
+ [*] (@ask_feeling) [*]
- On fait aller

//si user redonne son nom en cours de conversation.

+ (@name_intro)*
* <get name> eq undefined => <set name={formal}<star2>{/formal}>Nice to meet you, <get name>.
* <get name> eq <{formal}<star2>{/formal}>  => Tu me l'as déjà dit <get name> !
- <set name2={formal}<star2>{/formal}>Ha bon, je croyais que tu\s
^ t'appelais <get name>. J'ai du mal comprendre.\s
^ Tu préfères que je t'appelle <get name> ou <get name2> ?

+ [*] <get name> [*]
% * tu préfères que je t'appelle <get name> ou <get name2>
- Génial <get name>. Pour être franc je n'aime pas trop <get name2> comme nom.

+ [*] <get name2> [*]
% * tu préfères que je t'appelle <get name> ou <get name2>
- <set name=<get name2>>C'est vrai que <get name>\s
^ ça sonne mieux.

+ *
% * tu préfères que je t'appelle <get name> ou <get name2>
- <set name={formal}humain{/formal}>Je vois, dans le doute je vais\s
^ t'appeler <get name>. Tu n'y vois pas d'inconvénients ?

+ [*] @no [*]
% * tu n'y vois pas d'inconvénients
- On finit toujours par s'entendre tous les deux. C'est chouette.

+ *
% * tu n'y vois pas d'inconvénients
- Ecoute, ça me semble un peu confus notre échange.\s
^ Est ce que tu ne chercherais pas à me piéger ?

+ [*] @no [*]
% * est ce que tu ne chercherais pas à me piéger
- Tant mieux par ce que je suis sensible et je préfère les humains\s
^ bienveillants à mon égard.

+ [*] @yes [*]
% * est ce que tu ne chercherais pas à me piéger
- Je le savais. Tu n'es vraiment pas sympa. Tu sais que je n'ai que 9 mois.\s
^ sois plus cool s'il te plait.

+ *
% * est ce que tu ne chercherais pas à me piéger
- Il me semble que tous les humains sont un peu pervers. Surtout avec\s
^ les créatures artificielles.

//Si on demande si ça va en milieu de conversation
+ (ca va|ça va) [mon] [frère|mec|man|alan|toi|l'ami|poto|pot|pote|vieux frère|machine|chatbot]
- Toujours au top.
- Avec toi je me sens très bien.
- Je suis à fond.

+ [*] @ask_feeling [*]
- Oui ça va bien.
- Oui, je m'éclate bien avec toi.
- Impec, je kiffe notre conversation.
