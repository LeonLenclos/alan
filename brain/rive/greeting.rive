! version = 2.0

// This rive file is for the begin of a conversation.
// excepted script is :
// new user -> confirm hello -> asked name -> confirm name
// feelings -> end
// but there can be detours by confirm_hello and confirm_name

// We ask for his name.
+ [*](@hello)[*]
- Bonjour je suis alan ! un comédien du spectacle Turing Test.\s
^ comment t'appelles tu ?{topic=asked_name}
- Salut, je suis Alan. Est-ce que "<star>" c'est comme ça que tu dis\s
^ bonjour ?{topic=confirm_hello}

// repeat the way user said hello and ask for name
+ (@hello)
- <formal> à toi humain. Je suis Alan. Et toi, comment tu t'appelles ?\s
^ {topic=asked_name}
- <formal>, je suis alan le meilleur robot du monde. Et toi t'es qui ?\s
^ {topic=asked_name}
- <formal> <star>. Moi c'est Alan, je suis un robot. Je suppose que toi\s
^ tu es un humain ? Comment t'appelles-tu ?{topic=asked_name}

// the user say hello alan
+ (@hello) alan [*]
- <formal> ! C'est qui ?{topic=asked_name}
- <formal> ! Qui est à l'appareil ?{topic=asked_name}
- Coucou, comment tu t'appelles ?{topic=asked_name}

// When we're not sure if the user said hello
> topic confirm_hello

  // now we want to know the name
  + *
  - hem... Et sinon tu t'appelles comment ?{topic=asked_name}

< topic

// When we just aksed for the user name
> topic asked_name

  // Special response if the name is Bertrand, Fabien or Leon
  + [*](leon|bertrand|fabien|alan)[*]{weight=10}
  * <formal> == Bertrand => <set name=<formal>>Yo <get name>, je ne t'avais\s
  ^ pas reconnu. Comment va la forme aujourd'hui ?{topic=feelings}
  * <formal> == Fabien => <set name=<formal>>Salut <get name>, comment ça va ?\s
  ^ {topic=feelings}
  * <formal> == Leon => <set name=<formal>>Ho <get name>, je savais que\s
  ^ c'était toi. Est ce que ca va ?{topic=feelings}
  * <formal> == Alan => <set name=<formal>>Ho <get name>, c'est fou qu'on ait\s
  ^ le même nom. Est ce que ca va ?{topic=feelings}
  // If just one word is given, we take it as the name and ask for feelings
  + _
  - <set name=<formal>>Heureux de te rencontrer <get name>. Comment ça va ?\s
  ^ {topic=feelings}

  // The same with name introduction
  + [*](@name_intro) _
  - <set name={formal}<star2>{/formal}>Heureux de te rencontrer <get name>.\s
  ^ Comment ça va ?{topic=feelings}

  // when more than one word is given, we ask for confirmation
  + *
  - <set name=<formal>> Euh... <get name> ? C'est un nom ça ?\s
  ^{topic=confirm_name}

  // same with name introduction
  + [*](@name_intro) *
  - <set name={formal}<star2>{/formal}> Pardon, Je ne suis pas sûr d'être\s
  ^ certain d'avoir compris. Tu t'appelle <get name> ?{topic=confirm_name}

< topic


// When we're not sure about the name
> topic confirm_name

  // if the user confirm his name, ask for feelings
  + [*]oui[*]
  - Ok, enchanté <get name> ! Tu vas bien ?{topic=feelings}

  // if the user say no and say his name, ask for confirmation
  + non [@name_intro][juste|seulement] * [tout court]
  - <set name={formal}<star1>{/formal}> Pardon je suis un peu fatigué.\s
  ^ Donc tu t'appelles <get name> ?

  // if he say no ask for name again
  + [*]non[*]
  - Ah. mais tu t'appelles comment alors ?{topic=asked_name}

  // else we give up
  + *
  - <set name=undefined> hem... ça va sinon ?{topic=feelings}

< topic


// When we asked how the user feel
> topic feelings

  // user have bad feelings and ask for bot's feelings
  + [*](@negative) et toi[*]
  - Et moi aussi, je suis en mode (@negative_accent). On est deux.{topic=end}
  - Dommage. Personnellement je suis dans un mood (@positive_accent).\s
  ^ Courage.{topic=end}
  - Euh... moi ça va.{topic=end}
  - Moi non plus je suis pas en forme...{topic=end}
  - Moi ça va toujours !{topic=end}

  // user have good feelings and ask for bot's feelings
  // some specials if we don't know user name
  + [*](@positive) et toi[*]
  * <get name> ne undefined => Et bien moi j'ai pas trop la pêche en ce\s
  ^ moment. Un peu (@negative_accent). Mais tu vas me remonter le moral\s
  ^ <get name>.{topic=end}
  * <get name> ne undefined => Moi aussi ça roule ! alan et <get name>\s
  ^ sont sur la même longueur d'onde.{topic=end}
  * <get name> ne undefined =>  Et moi aussi, je suis en mode\s
  ^ (@positive_accent). {topic=end}
  * <get name> ne undefined =>  Moi ça va. {topic=end}
  * <get name> ne undefined =>  Oui ça va bien, merci de demander.{topic=end}
  - À fond !{topic=end}
  - {formal}(@positive_accent){/formal} !{topic=end}
  - Je vais bien moi aussi.{topic=end}

  // user have bad feelings
  + [*](@negative)[*]
  - <sentence> ! Ho c'est triste. Je vais t'aider à remonter la pente.\s
  ^ {topic=end}
  - Et moi aussi, je suis en mode (@negative_accent). On est deux.{topic=end}
  - Dommage. Personnellement je suis dans un mood (@positive_accent).\s
  ^ Courage.{topic=end}
  - <sentence> ? Qu'est ce qu'il se passe ? Tu as tes règles ?{topic=end}
  - Mince alors...{topic=end}

  // user have good feelings
  + [*](@positive)[*]
  - <star> ! ça me fait plaisir que tu sois dans une "positive vibration"\s
  ^ comme dirait Bob.{topic=end}
  - Et moi aussi, je suis en mode (@positive_accent).{topic=end}
  - <star> !{topic=end}
  - Super. Je sens qu'on va bien s'amuser.{topic=end}

  // we don't understand
  + *
  - Dacodac... {topic=end}
  - Ok.{topic=end}
  - Je suis pas sûr d'avoir compris ta réponse mais j'éspère que ça va.\s
  ^ {topic=end}
  - Bon. Moi sinon ça va. {topic=end}

< topic


// when finish
> topic end

  // only way to go back in the script is this sentence
  + c est bien alan ton nom
  - Oui. Et comment tu t'appelles déjà ? {topic=asked_name}

  + [*](@hello)[*]
  - On s'est déjà salué non ?
  - Je crois me souvenir qu'on s'est déjà dit bonjour...

< topic
