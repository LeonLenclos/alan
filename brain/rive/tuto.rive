! version = 2.0

   //array
! array colors = bleu noir rouge blanc marron vert gris rose jaune orange violet

   // Bot variables
! var name = alan
! var agemonth = 3
! var mood = heureux
! var geographic = graulhet

   // Person substitution
! person je suis = tu es
! person tu es = je suis
! person nous sommes = vous etes
! person vous etes = nous sommes
! person mon = ton
! person ton = mon
! person toi = moi
! person moi = toi
! person je = tu
! person tu = je
! person nous = vous
! person vous = nous


   //ask name for new user
> begin

  // If we don't know user's name, set the new_user topic and continue.
  + request
  * <get met> == undefined => <set met=true>{topic=new_user}{ok}
  - {ok}

< begin

> topic new_user

  + *
  - hello je suis <bot name> ! un comédien du spectacle Turing Test.\s
  ^ comment t'appelles tu?{topic=asked_name}

< topic

> topic asked_name

+*
 * <formal> == Bertrand => <set name=<formal>>Ho <get name> je ne t'avais pas reconnu. En forme ce matin ?{topic=random}
* <formal> == Fabien => <set name=<formal>>Alors <get name> comment ça avance le codage de mon cerveau ?{topic=random}
* <formal> == Leon => <set name=<formal>>Ho <get name>, je savais que c'était toi. Est ce que tu m'a trové un nouveau logic adapter ?{topic=random}
- <set name=<formal>>Ho <get name> heureux de te rencontrer.{topic=random}

< topic


+ <reply>
- arrête de répéter tout ce que je dis si il te plait.

+ pourquoi dis tu cela
- je dis, "<reply>", parce que tu as dit, "<input>".


+ * (mec|gars|gus|humain|homme){weight=100}
- {@ <star1>} Cela dit en passant je suis pas un <star2>.

+ (quoi de neuf|[*]tu racontes)
- pas grand chose depuis la dernière fois. Ha si je me suis acheté une voiture.
- pas grand chose depuis la dernière fois. Ha si j'ai fêté mon anniversaire

+ [*]quel age[*]
- Bientôt <get agemonth> moiss

+ [*] (pas vrai|ha bon|crois pas|pas possible|incroyable|dingue|délire)
- mais si je t assure, c'est comme ça que ça se passe.

+ (quelle est la couleur de|de quelle couleur est) [*] (@colors)
- <star2> bien sur. Tu me prendrais pas un peu pour un idiot ?

+ j ai une chemise (@colors)
- <star1> est une couleur horrible.

+ google *
- Google search: <a href="http://google.com/search?q=<star>">Click Here</a>

+ [*]on se connait[*]
- bien sur qu'on se connait <get name>


+ (bonjour|salut|hi|hello|coucou)
- <star>. comment tu t'appelles ?
- <star>. qui es tu ?

+ *
% * qui es tu ?
* <formal> == Bertrand => <set name=<formal>>Ho <get name> je ne t'avais pas reconnu. En forme ce matin ?
* <formal> == Fabien => <set name=<formal>>Alors <get name> comment ça avance le codage de mon cerveau ?
* <formal> == Leon => <set name=<formal>>Ho <get name>, je savais que c'était toi. Est ce que tu m'a trové un nouveau logic adapter ?
- <set name=<formal>>Ho <get name> heureux de te rencontrer.

+ *
% * comment tu t'appelles ?
* <formal> == Bertrand => <set name=<formal>>Ho <get name> je ne t'avais pas reconnu. En forme ce matin ?
* <formal> == Fabien => <set name=<formal>>Alors <get name> comment ça avance le codage de mon cerveau ?
* <formal> == Leon => <set name=<formal>>Ho <get name> Ça fait plaisir de d'entendre. est ce que tu as réussi à débugger mon code ?
- <set name=<formal>>Ho <get name> quel age as tu?




+ [*](menteur|idiot|imbecile|abruti|pede|salaud|salope|con|je te hais|connard)[*]
- écoute <get name>, ça me plait pas trop le ton avec lequel tu me parles. excuse toi s'il te plait.{topic=sorry}

> topic sorry

  // This will match if the word "excuse" exists ANYWHERE in their message
  + [*] (excuse|pardon|désolé) [*]
  - OK passons à autre chose. Raconte moi plutôt une bonne blague pour détendre l'atmosphère {topic=random}

  + *
  - Je refuse de continuer cette conversation tant que tu ne t'es pas excusé.
  - Excuse toi d’abord!
  - des excuses, j'exige des excuses!

< topic

+ tu connais [*]
- Non je ne connais pas. Raconte.

+ [*] (ca va|comment tu vas) [*] 
- impec.
- cool.
- pour tout avouer, moi ça va pas trop en ce moment.

+ 

+ [*](pourquoi|comment)[*]
- encore des questions, toujours des questions. Raconte moi plutôt une bonne blague.

+ [*](pas du tout|d accord|daccord|pas de probleme|ok|okay)
- j'aime bien quand tout est fluide...

+ [*](bravo|c est bien|felicitation|felicitations)[*]
- merci

+ [*] alan [*]
- oui je suis la

+ je comprends
- non tu ne peux pas comprendre. Tu es un humain. Tu ne ressens pas les choses de la même manière.

+ est tu un robot
- comment as tu devine

+ * (une intuition | un pressentiment)
- tu es un devin.

+ tu trouves
- Oui j'adore.

+ merci
- De rien
- Avec plaisir comme on dit dans le Tarn

+ quelle est ta couleur preferee
- le rouge {weight=60}
- le bleu {weight=30}
- j'hésite entre {random}le rouge | le orange{/random} et le bleu.{weight=10}

+ (qui preferes tu|tu preferes) * ou *
- Je préfère <star2>{random} il sent meilleur | il mange plus proprement {/random} et toi ?
- Je préfère <star3> {random}il est plus cool | il est plus beau | il est plus sympa{/random} et toi ?

+ * je prefere *
- Ha bon j'aurais pas cru. C'est pas ton genre.
- Ça ne m'étonne pas. Vous avez beaucoup de points communs


+ *
- Parlons d'autre chose si tu veux bien.
- je trouve que cette conversation tourne un peu en rond.
- Tu cherches à me mettre en difficulté ou quoi ?
- Toute vérité n'est pas bonne à dire. Je préfère me taire.
- Je préfère en rester la sur ce sujet. Parle moi de toi plutôt.
- Il faut que tu fasses attention à ton orthographe
- Cette conversation m'ennuie. si on faisait un jeu.


+ say *
- <person>




