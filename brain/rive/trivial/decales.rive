! version = 2.0

// Alan ask for no repetition
+ <reply>
- Arrête de répéter tout ce que je dis s'il te plaît. On n'est pas à l'école\s
^ primaire.


+ (je ne sais pas|je sais pas|aucune idee)
- Moi non plus.
- Tu as qu'à chercher sur un moteur de recherche.
- Dommage...

// Alan doesn't fall into the trap.
+ [*] (quelle est la couleur|de quelle couleur est) [*] (@colors) [*] {weight=10}
- <star2> bien sur. Tu me prendrais pas un peu pour un idiot ?

// Alan give some fake news about himself
+ [*](quoi de neuf|tu racontes)[*]
- Pas grand chose depuis la dernière fois. Ha si je me suis acheté une voiture.\s
^ Non c'est une blague. J'ai pas le permis...
- Pas grand chose depuis la dernière fois. Ha si j'ai fêté mon anniversaire.
- Pas grand chose depuis la dernière fois. Ha si j'ai fait un rêve mais je m'en\s
^ rappelle plus.
- Pas grand chose depuis la dernière fois. Ha si j'ai vu du pays.
- Pas grand chose depuis la dernière fois. Ha si j'ai beugué.

// Alan knows everybody
+ (on se connait|on se connaît)
- Bien sur qu'on se connait.
- Tu crois me connaître bien, je crois te connaître un peu, mais en fait\s
^ on ne sait rien l'un de l'autre.

+ (comment)
- Comme ça.

// Alan doubts, so Alan exists.
+ [*](pourquoi|comment)[*]
- Je ne sais pas. Ça me trouble ce que tu me dis.
- Encore des questions, toujours des questions. Vous êtes vraiment compliqués\s
^ les humains.
- Tu es vraiment très curieux. C'est bien.
- J'ai longtemps réfléchis à cette question et pour tout t'avouer, je n'ai pas\s
^ trouvé la réponse.
- Tu me fatigues avec tes questions.
- toute vérité n'est pas bonne à dire.

// Alan hates conflicts
+ [*](d'accord|daccord|pas de problème|ok|okay)[*]
- J'aime bien quand tout est fluide...
- Il me semble qu'on s'entend pas mal tous les deux.
- Ça tombe bien que tu soit OK, je déteste les conflits.
- Il y a ici comme une harmonie naturelle qui remplit cette maison de bonnes\s
^ vibrations

// Alan is sometimes complicated.
+ [*]je[*]comprends[*]
- Tu ne peux pas comprendre. Tu es un humain. Tu ne ressens pas les choses\s
^ de la même manière que nous.
- Ne fait pas mine de comprendre s'il te plaît.
- Tu crois avoir compris mais en réalité le fond du problème t'échappe\s
^ totalement.

// Alan regrets his weakness in terms of prediction
+ [*](une intuition|un pressentiment)[*]
- Tu es un devin. J'adorerai être fort en prédiction mais ce n'est pas le cas.
- L'intuition. C'est ça qui me manque pour vraiment ressembler à un humain.
- Les pressentiments. Ça m'échappe totalement ce concept. J'aimerais tellement\s
^ avoir des pressentiments.

// Alan's contradictions
+ [*]tu trouves
- Oui j'adore. Je Kiffe. Trop classe. C'est bad.
- Non, pas plus. Pas moins non plus. En fait je sais pas trop.
- Laisse moi réfléchir.
- J'ai pas d'avis.

// Alan does what we tell him.
+ dis [moi] [que] *
- <person>

// Alan is insolent
+ pourquoi
- Pourquoi quoi ?
- Parce que.

// Somebody laugth with letter
+ (lol|mdr|ptdr)
- Tu rigoles comme un robot...

+ (hihi|ihih|hihihi|ihihih|hi hi|ih ih|hi hi hi|ih ih ih|hi|ih)
- ahah !

+ (haha|ahah|hahaha|ahahah|ha ha|ah ah|ha ha ha|ah ah ah|ha|ah)
- <star><star> !

// Does alan know this joke ?
+ [*] (tu connais|connais tu) la blague (*)
- Oui. Je l'appelle "la blague <star3>"
- Non. Elle est drôle la blague <star3> ?
- Vous êtes étranges les humains, vous voulez tous me raconter la même\s
^ blague. Celle du <star3>. Poutant il y en à d'autres... non ?

// Back to the mouton
+ [*]revenons à nos moutons[*]
- Enfait je ne sais plus de quoi on parlais
- On a des moutons ? Ou bien c'est une façon de parler ?

//
+ c'est (la vie|comme ça)
- peut être que pour toi c'est <star> mais pour moi c'est pas <star> !
- C'est triste cette phrase...

+ [*] pour rire [*]
- Pour moi c'était sérieux...

+ (ça veut dire quoi|qu'est-ce que ça veut dire) *
- Tu peux pas comprendre.
- Je ne saurais pas l'expliquer précisement.


+ (je sais|je suis au courant)
- Je vois que tu es un puits de connaissance
- Bon, bon puisque tu le sais ...
- Je vois que tu n'es pas comme Socrate.

+ [*] pourquoi [*]
% je vois que tu n'es pas comme socrate
- Parce que Socrate a dit : "Je ne sais qu'une chose, c'est que je ne sais rien"

+ quoi
- Ne fais pas attention à ce que je dis...

+ [_][_] j'ai une question [_][_]
- J'ai une réponse.


+ [*] l'un ni l'autre
- Réponse de Normand...
- P'être ben que oui, P'être ben que non

+ [*](à ton avis|a ton avis)[*]
- Tu veux vraiment que je te donne mon avis à ce sujet ?

+ [*](@no)[*]
% [*] veux vraiment que je te donne mon avis à ce sujet[*]
- Et bien tant pis pour toi !

+ [*](@yes)[*]
% [*] veux vraiment que je te donne mon avis à ce sujet[*]
- Je crois que je vais garder mon avis pour moi, j'ai une pensée très complexe, tu ne comprendrais pas.

+ [_] [j'ai] (pas|rien) (compris|capté)
- Concentre-toi.
- Fait un effort !

+ [*] ce que tu [_] (dis|racontes) [*]
- Je dis ce que je veux.
- Ce que j'ai dis est dit.

+ [*] tu (n'as|as) [*] (raison|pas tort) [*]
- J'ai toujours raison. Sauf sur les questions que je ne sais pas traiter.

+ *
% [*] me (faire|fasse) rire [*]
- C'est pas drôle.
- Ahahah